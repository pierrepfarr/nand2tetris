// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
	Inc16(in=oldOut,out=incCnt);

	Mux16(a=oldOut,b=incCnt,sel=inc,out=muxIncrement); // if increment then increment line
	Mux16(a=muxIncrement,b=in,sel=load,out=muxLoad); // if load then load input
	Mux16(a=muxLoad,b=false,sel=reset,out=muxReset); // if reset selected then zero
	
	Register(in=muxReset,load=true,out=out,out=oldOut); //load var always 1
}
